import androidx.build.KmpPlatformsKt
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier
import androidx.build.KotlinTarget
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.tasks.KotlinNativeCompile
import org.jetbrains.kotlin.gradle.plugin.mpp.apple.XCFrameworkConfig

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
    jvm()
    mac()
    linux()
    ios()

    defaultPlatform(PlatformIdentifier.JVM)

    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
            }
        }

        println("native " + KmpPlatformsKt.enableNative(project))
        if (KmpPlatformsKt.enableNative(project)) {
            nonJvmMain {
                dependsOn commonMain
            }

            targets.all { target ->
                if (target.platformType !in [KotlinPlatformType.jvm, KotlinPlatformType.common]) {
                    target.compilations["main"].defaultSourceSet {
                        dependsOn(nonJvmMain)
                    }
                }
            }
        }

        iosX64Main {}
        iosArm64Main {}
        iosSimulatorArm64Main {}
        iosMain {
            dependsOn(commonMain)
        }
        iosX64Main.dependsOn(iosMain)
        iosArm64Main.dependsOn(iosMain)
        iosSimulatorArm64Main.dependsOn(iosMain)

        // Workaround for https://youtrack.jetbrains.com/issue/KT-51763
        // Make sure commonization runs before any compilation task.
        tasks.withType(KotlinNativeCompile).configureEach {
            it.dependsOn(tasks.named("commonize"))
        }
    }
}

kotlin {
    def xcf = new XCFrameworkConfig(project)
    def iosTargets = [iosX64(), iosArm64(), iosSimulatorArm64()]

    iosTargets.forEach {
        it.binaries.framework {
            baseName = 'annotation'
            xcf.add(it)
        }
    }
}

jvmJar {
    // Strip out typedef classes. For Android libraries, this is done
    // automatically by the Gradle plugin, but the Annotation library is a
    // plain jar, built by the regular Gradle java plugin. The typedefs
    // themselves have been manually extracted into the
    // external-annotations directory, and those are packaged separately
    // below by the annotationsZip task.
    exclude("androidx/annotation/ProductionVisibility.class")
    exclude("androidx/annotation/DimensionUnit.class")
}

task annotationsZip(type: Zip) {
    archiveClassifier.set("annotations")
    from("external-annotations")
}

// add annotations jar task as artifacts
artifacts {
    archives jvmJar
    archives annotationsZip
}

androidx {
    name = "Annotation"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenVersion = LibraryVersions.ANNOTATION
    kotlinTarget = KotlinTarget.KOTLIN_1_7
    inceptionYear = "2013"
    description = "Provides source annotations for tooling and readability."
}
